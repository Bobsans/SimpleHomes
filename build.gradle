buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'

repositories {
    maven {
        name = "GitHubPackages"
        url = uri('https://maven.pkg.github.com/Bobsans/BobLib')
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
        }
    }
}

version = mod_version
group = group_name
archivesBaseName = "[${minecraft_version}] ${mod_name}"

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            properties 'forge.logging.console.level': 'info'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }

        server {
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            properties 'forge.logging.console.level': 'info'
            workingDirectory project.file('run').canonicalPath
            source sourceSets.main
        }

        data {
            properties 'forge.logging.markers': 'SCAN,REGISTRIES,REGISTRYDUMP'
            properties 'forge.logging.console.level': 'debug'
            workingDirectory project.file('run').canonicalPath
            args '--mod', 'simplehomes', '--all', '--output', file('src/generated/resources/')
            source sourceSets.main
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    compile fg.deobf("by.bobsans.boblib:BobLib:${boblib_version}")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/mods.toml'

        expand([
                'mod_id'               : mod_id,
                'mod_version'          : mod_version,
                'mod_name'             : mod_name,
                'mod_author'           : mod_author,
                'mod_url'              : mod_url,
                'mod_update_url'       : mod_update_url,
                'mod_credits'          : mod_credits,
                'mod_description'      : mod_description,
                'boblib_version'       : boblib_version,
                'boblib_version_map'   : boblib_version_map,
                'minecraft_version'    : minecraft_version,
                'minecraft_version_map': minecraft_version_map,
                'forge_version'        : forge_version,
                'forge_version_map'    : forge_version_map
        ])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/mods.toml'
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "${mod_name}",
                "Specification-Vendor"    : "${mod_author}",
                "Specification-Version"   : "${minecraft_version}-${mod_version}",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "${mod_author}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
